plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.3.RELEASE'
  id 'org.springframework.boot' version '1.5.17.RELEASE'
  id 'org.owasp.dependencycheck' version '3.3.4'
  id 'com.github.ben-manes.versions' version '0.15.0'
  id "org.sonarqube" version "2.5"
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle.toolVersion = '8.2'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        // checkstyle not important for this demo for now
//        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
}

def springBootVersion = '1.5.17.RELEASE'
def springfoxSwagger = '2.9.2'
def javaLoggingVersion = '2.1.1'

repositories {
  jcenter()
  mavenCentral()
  maven {
    url  "https://dl.bintray.com/hmcts/hmcts-maven"
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: springBootVersion
  compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
  compile group: 'org.flywaydb', name: 'flyway-core', version: '4.1.2'


  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: javaLoggingVersion
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: javaLoggingVersion
  compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: '1.1.0'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: '1.4.6.RELEASE'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: '1.4.6.RELEASE'
  compile group: 'io.springfox', name: 'springfox-swagger2', version: springfoxSwagger

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
  testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.6'
  testCompile group: 'com.typesafe', name: 'config', version: '1.3.2'
}

jar {
  archiveName 'moj-rhubarb-recipes-service.jar'
}

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }
  functional {
    java.srcDir 'src/test/functional/java'
    resources.srcDir 'src/test/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
  apiGateway {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir 'src/test/gateway/java'
    }

    resources.srcDir 'src/test/gateway/resources'
  }
}

configurations {
  functionalCompile.extendsFrom testCompile
  functionalRuntime.extendsFrom testRuntime
  apiGatewayCompile.extendsFrom testCompile
  apiGatewayRuntime.extendsFrom testRuntime
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.reform.rhubarb.functional.SmokeTest'
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath
}

task apiGateway(type: Test, description: 'Runs the smoke tests for API gateway.', group: 'Verification') {
  testClassesDirs = sourceSets.apiGateway.output.classesDirs
  classpath = sourceSets.apiGateway.runtimeClasspath
}

sonarqube {
  properties {
    property "sonar.projectName", "moj-rhubarb-recipes-service"
    property "sonar.projectKey", "moj-rhubarb-recipes-service"
  }
}
