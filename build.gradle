plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.flywaydb.flyway' version '5.2.4'
  id 'org.springframework.boot' version '2.3.4.RELEASE'
  id 'org.owasp.dependencycheck' version '6.0.2'
  id 'com.github.ben-manes.versions' version '0.33.0'
  id 'org.sonarqube' version '2.7.1'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

pmd {
  toolVersion = "6.24.0"
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

sourceCompatibility = '11'

checkstyle {
  toolVersion = '8.29'
  configFile = new File(rootDir, "checkstyle.xml")
  maxWarnings = 0
}

ext {
  lombokVersion = '1.18.8'
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

def springBootVersion = springBoot.class.package.implementationVersion
def springfoxSwagger = '2.9.2'
def javaLoggingVersion = '5.1.0'

repositories {
  jcenter()
  mavenCentral()
  maven {
    url  "https://dl.bintray.com/hmcts/hmcts-maven"
  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

ext["rest-assured.version"] = '4.3.0'

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
        details.useVersion '2.10.1'
      }

      if (details.requested.name == 'jackson-databind') {
        details.useVersion '2.10.1'
      }

      if (details.requested.group in ['org.apache.tomcat.embed']) {
        details.useVersion '9.0.39'
      }

      if (details.requested.name in ['jaxb-osgi']) {
        details.useVersion '2.3.3'
      }
    }
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: springBootVersion
  compile group: 'org.postgresql', name: 'postgresql', version: '42.2.14'
  compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.1.0'

  compile group: 'com.google.guava', name: 'guava', version: '30.0-jre'

  compile group: 'uk.gov.hmcts.reform', name: 'logging', version: javaLoggingVersion
  compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: javaLoggingVersion
  compile group: 'io.springfox', name: 'springfox-swagger2', version: springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springfoxSwagger

  compile 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion, {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testCompile group: 'io.rest-assured', name: 'rest-assured'
  testCompile group: 'com.typesafe', name: 'config', version: '1.4.1'
}

mainClassName = 'uk.gov.hmcts.reform.rhubarb.recipes.Application'

bootJar {
  archiveFileName = 'moj-rhubarb-recipes-service.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }
  functional {
    java.srcDir 'src/test/functional/java'
    resources.srcDir 'src/test/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
  apiGateway {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir 'src/test/gateway/java'
    }

    resources.srcDir 'src/test/gateway/resources'
  }
}

configurations {
  functionalCompile.extendsFrom testCompile
  functionalRuntime.extendsFrom testRuntime
  apiGatewayCompile.extendsFrom testCompile
  apiGatewayRuntime.extendsFrom testRuntime
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.reform.rhubarb.functional.SmokeTest'
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath
}

task apiGateway(type: Test, description: 'Runs the smoke tests for API gateway.', group: 'Verification') {
  testClassesDirs = sourceSets.apiGateway.output.classesDirs
  classpath = sourceSets.apiGateway.runtimeClasspath
}

sonarqube {
  properties {
    property "sonar.projectName", "cnp-plum-recipes-service"
    property "sonar.projectKey", "cnp-plum-recipes-service"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
    property "sonar.coverage.exclusions", "**/config/**, **Exception**"
  }
}

test {
  useJUnitPlatform()
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

